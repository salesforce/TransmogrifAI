buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
        classpath 'com.commercehub.gradle.plugin:gradle-avro-plugin:0.8.0'
      //classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:0.8.2'
    }
}

repositories {
    maven {
       url "https://raw.githubusercontent.com/salesforce/op/mvn-repo/releases"
       credentials {
           // Generate github api token here - https://goo.gl/ANZ9oz
           // and then set it as an environment variable `export GITHUB_API_TOKEN=<MY_TOKEN>`
           username = System.getenv("GITHUB_API_TOKEN")
           password "" // leave the password empty
       }
       authentication { digest(BasicAuthentication) }
    }
    mavenCentral()
    maven { url "https://jitpack.io" }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'com.github.johnrengelman.shadow'
//apply plugin: 'scalaStyle'
apply plugin: 'com.commercehub.gradle.plugin.avro'
apply from: 'spark.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// The main class for the application plugin and is currently unused. Setting a dummy one.
mainClassName = "com.salesforce.app.Simple" /* << MAIN_CLASS */

ext {
    scalaVersion = '$scalaVersion'
    scalaVersionRevision = '$scalaVersionRevision'
    junitVersion = '$junitVersion'
    sparkVersion = '$sparkVersion'
    scalatestVersion = '$scalatestVersion'
    opVersion = '$opVersion'

    mainClassName = 'com.salesforce.app.Simple' /* << MAIN_CLASS */
}

configurations {
    scalaLibrary
    scalaCompiler
}

dependencies {
    // Scala
    scalaLibrary "org.scala-lang:scala-library:$scalaVersion.$scalaVersionRevision"
    scalaCompiler "org.scala-lang:scala-compiler:$scalaVersion.$scalaVersionRevision"
    compile "org.scala-lang:scala-library:$scalaVersion.$scalaVersionRevision"

    // Spark
    compileOnly "org.apache.spark:spark-core_$scalaVersion:$sparkVersion"
    testCompile "org.apache.spark:spark-core_$scalaVersion:$sparkVersion"
    compileOnly "org.apache.spark:spark-mllib_$scalaVersion:$sparkVersion"
    testCompile "org.apache.spark:spark-mllib_$scalaVersion:$sparkVersion"
    compileOnly "org.apache.spark:spark-sql_$scalaVersion:$sparkVersion"
    testCompile "org.apache.spark:spark-sql_$scalaVersion:$sparkVersion"

    // Optimus Prime
    compile "com.salesforce:optimus-prime-core_$scalaVersion:$opVersion"

    // Test
    testCompile ("org.scalatest:scalatest_$scalaVersion:${scalatestVersion}")
    testCompile "junit:junit:$junitVersion"

    // Avro
    compileOnly("org.apache.avro:avro:$avroVersion") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    testCompile("org.apache.avro:avro:$avroVersion") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    compileOnly("org.apache.avro:avro-mapred:$avroVersion:$hadoopVersion") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    testCompile("org.apache.avro:avro-mapred:$avroVersion:$hadoopVersion") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }

    // Spark Avro
    compile ("com.databricks:spark-avro_$scalaVersion:$sparkAvroVersion") { exclude group: "org.apache.avro", module: "avro" }

}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxMetaspaceSize=256m']
    }
}
compileScala { scalaCompileOptions.additionalParameters = ["-optimize"] }
compileTestScala { scalaCompileOptions.additionalParameters = ["-Yrangepos"] }
[compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }

jar {
    manifest.attributes "Main-Class": "\$mainClassName"
    baseName="\${rootProject.name}"
}

shadowJar {
    zip64 = true
    exclude 'META-INF/**'
    manifest.attributes "Main-Class": "\$mainClassName"
    baseName = project.name + '-with-dependencies'
}

avro {
    createSetters = true
    fieldVisibility = "PUBLIC_DEPRECATED"
    outputCharacterEncoding = "UTF-8"
    stringType = "String"
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-main-avro-java'
        }
    }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

task repl(type: JavaExec) {
    description 'Start Scala repl.'
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath
    standardInput System.in
    args '-usejavacp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    description "Sets the Gradle wrapper version to $gradleVersion."
}
