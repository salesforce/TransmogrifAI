buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.12:1.0.1'
        classpath 'com.commercehub.gradle.plugin:gradle-avro-plugin:0.16.0'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.scoverage' version '2.5.0'
}
repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'scalaStyle'
apply plugin: 'com.commercehub.gradle.plugin.avro'

apply from: 'gradle/tests.gradle'
apply from: 'gradle/spark.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// The main class for the application plugin and is currently unused. Setting a dummy one.
mainClassName = "please.set.main.class.in.build.gradle"

ext {
    junitVersion = '4.12'
    sparkVersion = '3.1.1'
    scalatestVersion = '3.0.0'
    transmogrifaiVersion ='0.8.0'
    collectionsVersion = '3.2.2'
    scoveragePluginVersion = '1.3.1'
}

//noinspection GroovyAssignabilityCheck
configurations {
    scalaLibrary
    scalaCompiler
}

//noinspection GroovyAssignabilityCheck
dependencies {
    // Scala
    zinc 'com.typesafe.zinc:zinc:0.3.15'
    scoverage "org.scoverage:scalac-scoverage-plugin_%%:$scoveragePluginVersion"
    scoverage "org.scoverage:scalac-scoverage-runtime_%%:$scoveragePluginVersion"
    scalaLibrary "org.scala-lang:scala-library:%scala-version%"
    scalaCompiler "org.scala-lang:scala-compiler:%scala-version%"
    compile "org.scala-lang:scala-library:%scala-version%"

    // Spark
    compileOnly "org.apache.spark:spark-core_%%:$sparkVersion"
    testCompile "org.apache.spark:spark-core_%%:$sparkVersion"
    compileOnly "org.apache.spark:spark-mllib_%%:$sparkVersion"
    testCompile "org.apache.spark:spark-mllib_%%:$sparkVersion"
    compileOnly "org.apache.spark:spark-sql_%%:$sparkVersion"
    testCompile "org.apache.spark:spark-sql_%%:$sparkVersion"

    // TransmogrifAI
    compile "com.salesforce.transmogrifai:transmogrifai-core_%%:$transmogrifaiVersion"

    // Pretrained models used in TransmogrifAI, e.g. OpenNLP POS/NER models etc. (optional)
    // compile "com.salesforce.transmogrifai:transmogrifai-models_%%:$transmogrifaiVersion"

    // Test
    testCompile "org.scalatest:scalatest_%%:$scalatestVersion"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.scoverage:scalac-scoverage-plugin_%%:$scoveragePluginVersion"
    testCompile "org.scoverage:scalac-scoverage-runtime_%%:$scoveragePluginVersion"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy {
        force "commons-collections:commons-collections:$collectionsVersion",
                "org.scala-lang:scala-library:%scala-version%",
                "org.scala-lang:scala-reflect:%scala-version%"
    }
}
configurations.zinc {
    resolutionStrategy {
        force 'org.scala-lang:scala-library:2.10.6', 'org.scala-lang:scala-reflect:2.10.6'
    }
}


tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxMetaspaceSize=256m']
    }
}
compileScala { scalaCompileOptions.additionalParameters = ["-optimize"] }
compileTestScala { scalaCompileOptions.additionalParameters = ["-Yrangepos"] }
[compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }

jar {
    manifest.attributes "Main-Class": "$mainClassName"
    baseName = "${rootProject.name}"
}

shadowJar {
    zip64 = true
    exclude 'META-INF/**'
    baseName = jar.baseName
}

scalaStyle {
    configLocation = "${rootProject.projectDir}/gradle/scalastyle-config.xml"
    includeTestSourceDirectory = true
    source = "src/main/scala"
    testSource = "src/test/scala"
}
compileScala.dependsOn(scalaStyle)

avro {
    createSetters = true
    fieldVisibility = "PUBLIC_DEPRECATED"
    outputCharacterEncoding = "UTF-8"
    stringType = "String"
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-main-avro-java'
        }
    }
    scoverage {
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.main.output
    }
    testScoverage {
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        compileClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

wrapper {
    gradleVersion = '5.2'
    distributionType = Wrapper.DistributionType.BIN
    distributionSha256Sum = 'ff322863250159595e93b5a4d17a6f0d21c59a1a0497c1e1cf1d53826485503f'
}

task runMain(type: JavaExec) {
    description 'run a main class, i.e.: runMain -Dmain=MyMainClass -Dargs="arg1 arg2 arg3"'
    main = System.getProperty("main")
    classpath = sourceSets.main.runtimeClasspath
    //noinspection GroovyAssignabilityCheck
    systemProperties = System.getProperties()
    args = System.getProperty("args", "") == "" ? new LinkedList<String>() : Arrays.asList(System.getProperty("args").split(" "))
}

task repl(type: JavaExec) {
    description 'Start Scala repl.'
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath + configurations.scalaCompiler
    standardInput System.in
    args '-usejavacp'
}

task classpath {
    doLast {
        description 'Print project classpath.'
        println sourceSets.main.runtimeClasspath.asPath
    }
}
